#!/bin/csh -fv

#**********************************************************************
# Run SCAM with a single IOP
#    This script will build and run one IOP
#    If a user wishes to run more than one IOP, use create_scam6_iop_multi
#
# Usage:
#   ./create_scam6_iop <IOP>    # where IOP name is from list below
#      - or -
#   ./create_scam6_iop          # IOP is specified in the script below
#**********************************************************************

#------------------
# User sets options in this section
#------------------

### Full path of cesm source code and case (output) directories (see examples)

# set CESMDIR=/project/amp/$USER/collections/cam5_4_175
# set CASEDIR=/project/amp/$USER/cases
set CESMDIR=/opt/ncar/cesm2
set CASEDIR=/home/scam/work/cases
mkdir -p ${CASEDIR}

### Case Name
# OPT_TEMP should be set to 240K, 263K, or 273K
set OPT_TEMP=263K
set CASETITLE=cri${OPT_TEMP}_test
echo $CASETITLE

### Set location of user source mods (if any)
setenv this_dir  `pwd`
setenv usrsrc  ${this_dir}/mods

### Standard Run Settings
set RES=T42_T42
set COMPSET=FSCAM
set COMPILER=gnu

### Set Desired IOP
### $1 means read from command line. Or put one of the names in:
###     arm95 arm97 atex bomex cgilsS11 cgilsS12 cgilsS6 dycomsRF01 dycomsRF02 gateIII mpace rico sparticus togaII twp06

if ($#argv == 0) then
  set IOP = mpace
else
  set IOP = $1
endif

#------------------
# User should not need to set any options in this section
#------------------

cd  $CASEDIR

set IOPNAME = scam_$IOP

## location of IOP data in CESM Tag
set MODSDIR = $CESMDIR/components/cam/cime_config/usermods_dirs

#Create full casename
set CASENAME=${CASETITLE}.${COMPSET}.${IOP}

#------------------
# create case
#------------------

$CESMDIR/cime/scripts/create_newcase --compset $COMPSET  --res $RES --compiler $COMPILER --case $CASEDIR/$CASENAME  --user-mods-dir ${MODSDIR}/${IOPNAME} --run-unsupported --mach ncar-scam-container

cd  $CASEDIR/$CASENAME

### Set build and run directories to be under case directory.

set RUNDIR=${CASEDIR}/${CASENAME}/run
./xmlchange RUNDIR=$RUNDIR

./xmlchange EXEROOT=${CASEDIR}/${CASENAME}/bld

#------------------
# XMLCHANGE OPTIONS HERE
#------------------

### Append to CAM configure options
# ./xmlchange --append CAM_CONFIG_OPTS=' '

### DEBUG
./xmlchange DEBUG='TRUE'

#------------------
# Setup Case
#------------------

./case.setup 
# ./case.setup -d -v    #-d -v for verbose and debug file

#------------------
#  source mods: copy them into case directory
#------------------

/bin/cp -f  ${usrsrc}/* SourceMods/src.cam/

#------------------
# Add all user specific cam namelist changes here
#
# Users should add all user specific namelist changes below in the form of
#    namelist_var = new_namelist_value
# Namelist settings which appear in usermods_dir and here will use the values
#    specified below
# Other namelist settings from usermods_dirs will be unchanged
# Output can also be specified here (e.g. fincl1)
#------------------

cat >> user_nl_cam << EOF
 use_topo_file          = .true.  
 mfilt          = 2500
 nhtfrq         = 1
 fincl1= 'CDNUMC', 'AQSNOW','ANSNOW','FREQSL','LS_FLXPRC','LWC','EMIS','TOT_CLD_VISTAU','FREQSLM','FREQM','FREQMIX','SU','SD','LU','LD'
 prescribed_strataero_cycle_yr          = 2000
 prescribed_strataero_datapath          = "\$DIN_LOC_ROOT/atm/cam/ozone"
 prescribed_strataero_file               = 'ozone_strataero_CAM6chem_2000climo_zm_5day_c171004.nc'
 liqopticsfile             = "/home/scam/work/rfn_cri/correct_optics/CESM_CRI_RFN_${OPT_TEMP}.nc"
 spectralflux      = .true.
EOF

cat >> user_nl_clm << EOFclm
stream_fldfilename_lai = ''
EOFclm

#------------------
# Build
#------------------

#./case.build 
./case.build -d -v   #-d -v for verbose and debug file

### make timing dir kludge  [REMOVE WHEN FIXED]
mkdir -p $RUNDIR/timing/checkpoints

#------------------
# Choose type of job submission (batch or interactive)
#------------------

### Submit to Queue (If you have one)
#./case.submit

###  OR you can run interactively instead of going through the queue
cd $RUNDIR  
../bld/cesm.exe 



